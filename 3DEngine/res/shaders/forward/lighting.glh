uniform vec3 P_eyePos;
uniform float specularIntensity;
uniform float specularPower;
uniform sampler2D normal;
uniform sampler2D normal2;
uniform sampler2D normal3;
uniform sampler2D normal4;

struct BaseLight
{
    vec3 color;
    float intensity;
};

struct Attenuation
{
    float constant;
    float linear;
    float exponent;
};

struct DirectionalLight
{
    BaseLight base;
    vec3 direction;
};

struct PointLight
{
    BaseLight base;
    Attenuation atten;
    vec3 position;
    float range;
};

struct SpotLight
{
    PointLight pointLight;
    vec3 direction;
    float cutoff;
};

vec4 getNormalColor(vec3 splattingColor, vec2 texCoord){
    vec4 normalColor = texture2D(normal, texCoord.xy);
    normalColor = mix(normalColor, texture2D(normal2, texCoord.xy), splattingColor.r);
    normalColor = mix(normalColor, texture2D(normal3, texCoord.xy), splattingColor.g);
    normalColor = mix(normalColor, texture2D(normal4, texCoord.xy), splattingColor.b);
    return normalColor;
}

vec3 calcBumpedNormal(vec3 normals, vec3 tangents, vec3 normalMap){
    vec3 Normal = normalize(normals);
    vec3 Tangent = normalize(tangents);
    Tangent = normalize(Tangent - dot(Tangent, Normal) * Normal);
    vec3 BiTangent = cross(Tangent, Normal);
    vec3 BumpMapNormal = 2.0 * normalMap - vec3(1.0, 1.0, 1.0);
    vec3 NewNormal;
    mat3 TBN = mat3(Tangent, BiTangent, Normal);
    NewNormal = TBN * BumpMapNormal;
    NewNormal = normalize(NewNormal);
    return NewNormal;
}

vec4 CalcLight(BaseLight base, vec3 direction, vec3 normals, vec3 worldPos, vec2 texCoord, vec3 tangents, vec3 splattingColor)
{

    vec3 NormalMap = getNormalColor(splattingColor, texCoord).xyz;
    
    vec3 finalNormal;
    
    if(NormalMap != vec3(1,1,1)){
        finalNormal = calcBumpedNormal(normals, tangents, NormalMap);
    }else{
        finalNormal = normals;
    }
    
    float diffuseFactor = dot(finalNormal, -direction);
    
    vec4 diffuseColor = vec4(0,0,0,0);
    vec4 specularColor = vec4(0,0,0,0);
    
    if(diffuseFactor > 0)
    {
        diffuseColor = vec4(base.color, 1.0) * base.intensity * diffuseFactor;
        
        vec3 directionToEye = normalize(P_eyePos - worldPos);
        //vec3 reflectDirection = normalize(reflect(direction, normal));
        vec3 halfDirection = normalize(directionToEye - direction);
        
        float specularFactor = dot(halfDirection, finalNormal);
        //float specularFactor = dot(directionToEye, reflectDirection);
        specularFactor = pow(specularFactor, specularPower);
        
        if(specularFactor > 0)
        {
            specularColor = vec4(base.color, 1.0) * specularIntensity * specularFactor;
        }
    }
    
    return diffuseColor + specularColor;
}

vec4 CalcPointLight(PointLight pointLight, vec3 normal, vec3 worldPos, vec2 texCoord, vec3 tangents, vec3 splattingColor)
{
    vec3 lightDirection = worldPos - pointLight.position;
    float distanceToPoint = length(lightDirection);
    
    if(distanceToPoint > pointLight.range)
        return vec4(0,0,0,0);
    
    lightDirection = normalize(lightDirection);
    
    vec4 color = CalcLight(pointLight.base, lightDirection, normal, worldPos, texCoord, tangents, splattingColor);
    
    float attenuation = pointLight.atten.constant +
                         pointLight.atten.linear * distanceToPoint +
                         pointLight.atten.exponent * distanceToPoint * distanceToPoint +
                         0.0001;
                         
    return color / attenuation;
}

vec4 CalcSpotLight(SpotLight spotLight, vec3 normal, vec3 worldPos, vec2 texCoord, vec3 tangents, vec3 splattingColor)
{
    vec3 lightDirection = normalize(worldPos - spotLight.pointLight.position);
    float spotFactor = dot(lightDirection, spotLight.direction);
    
    vec4 color = vec4(0,0,0,0);
    
    if(spotFactor > spotLight.cutoff)
    {
        color = CalcPointLight(spotLight.pointLight, normal, worldPos, texCoord, tangents, splattingColor) *
                (1.0 - (1.0 - spotFactor)/(1.0 - spotLight.cutoff));
    }
    
    return color;
}

vec4 CalcDirectionalLight(DirectionalLight directionalLight, vec3 normal, vec3 worldPos, vec2 texCoord, vec3 tangents, vec3 splattingColor)
{
    return CalcLight(directionalLight.base, -directionalLight.direction, normal, worldPos, texCoord, tangents, splattingColor);
}
